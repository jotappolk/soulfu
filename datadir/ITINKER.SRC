#define SMOKE_COLOR 128

//-----------------------------------------------------------------------
int GetName()
  STANDARD.ItemName("TINKER", NULL, 255, 0, 0, 0, 0)

//-----------------------------------------------------------------------
int DamageIfFailed(int expression)
  if(expression > 0)
    return (expression<<1)
  return 0

//-----------------------------------------------------------------------
// Little helper...  Returns the percent chance to not
// explode, given a tinker-type and the character's intelligence...
// Chance increases by 2.5% with each point of intelligence...
int ChanceOfWorking(int expression, int intelligence)
  int chance
  chance = 200
  if(expression == 0)
    // Center (no tinker)
    return 100
  if(expression == 1 && intelligence >= 15)
    // Ammo
    chance = 25
  if(expression == 2 && intelligence >= 20)
    // Gonne
    chance = 30
  if(expression == 3 && intelligence >= 20)
    // Gronnade
    chance = 35
  if(expression == 4 && intelligence >= 25)
    // Flamer
    chance = 40
  if(expression == 5 && intelligence >= 25)
    // Tanke 1
    chance = 45
  if(expression == 6 && intelligence >= 25)
    // Hedgehog
    chance = 50
  if(expression == 7 && intelligence >= 25)
    // Tanke 2
    chance = 55
// !!!BAD!!!
// !!!BAD!!! Copter needs some polishing...
// !!!BAD!!!
  if(expression == 8 && intelligence >= 30)
    // Copter
    chance = 60



  chance = 200 - ((chance - intelligence)*5)
  chance = chance>>1
  if(chance > 95 && expression > 3)
    chance = 95
  if(chance > 90 && expression > 4)
    chance = 90
  if(chance > 100)
    chance = 100
  if(chance < 0)
    chance = 0
  return chance

//-----------------------------------------------------------------------
// Called when item is first used...
int Event()
  int self, child, expression, chance
  float z
  self = FindSelf()


  if(CharacterScriptFile(FindIndex(self)) != "FILE:CPLAYER.RUN")
    return FALSE


  if(self.action == ACTION_STAND || self.action == ACTION_WALK || self.action == ACTION_JUMP || self.action == ACTION_DOUBLE || self.action == ACTION_SPECIAL_10 || self.action == ACTION_SPECIAL_11)
    if(self.daction != ACTION_SPECIAL_11)
      PlaySound("FILE:-GNOME05.RAW", 256, 255)
    self.daction = ACTION_SPECIAL_11
    if(self.action != ACTION_SPECIAL_11)
      self.action = ACTION_SPECIAL_10
    else
      PDUST.Effect(1)


      z = self.z-0.5+self.height
      child = PEXCLAIM.Effect(self.x, self.y, z, 2.0, TRUE)
      if(child)
        expression = ITALK.ExpressionFromAxis(self.baxis)
        if(expression == 0)
          child.image = "FILE:=TINKASK.RGB" // Center - Question Mark
        if(expression == 1)
          child.image = "FILE:=TINKAMM.RGB" // Up - Ammo
        if(expression == 2)
          child.image = "FILE:=TINKGON.RGB" // Up Right - Gonne
        if(expression == 3)
          child.image = "FILE:=TINKGRO.RGB" // Right - Gronnade
        if(expression == 4)
          child.image = "FILE:=TINKFLA.RGB" // Down Right - Flamer
        if(expression == 5)
          child.image = "FILE:=TINKTAN.RGB" // Down - Tanke 1 (Tanke)
        if(expression == 6)
          child.image = "FILE:=TINKHOG.RGB" // Down Left - HedgeHog
        if(expression == 7)
          child.image = "FILE:=TINKREC.RGB" // Left - Tanke 2 (Recon)
        if(expression == 8)
          child.image = "FILE:=TINKCOP.RGB" // Up Left - Copter


        // Draw the chance to succeed bulbs...
        if(expression != 0)
          chance = ChanceOfWorking(expression, self.int)


//          StringClear(DEBUG_STRING)
//          StringAppend(DEBUG_STRING, "Chance of Working == ", 256)
//          StringAppendNumber(DEBUG_STRING, chance, 256)
//          StringAppend(DEBUG_STRING, "%, Damage == ", 256)
//          StringAppendNumber(DEBUG_STRING, DamageIfFailed(expression), 256)


          child = PEXCLAIM.Effect(self.x, self.y, z, 3.5, TRUE)
          if(child)
            child.width = 1.0
            child.length = 1.0
            child.image = "FILE:=DIMBULB.RGB"
          child = PEXCLAIM.Effect(self.x, self.y, z+0.15, 3.5, TRUE)
          if(child)
            child.width = 0.5 + (chance*0.01)
            child.length = child.width
            child.image = "FILE:LITESPRK.RGB"
            child.flags = child.flags | PART_LIGHT_ON
            child.alpha = (chance * 5) >> 1
            if(chance >= 100)
              child.alpha = 255
              child.blue = 128


//-----------------------------------------------------------------------
// Called when item is unpressed, to start proper animation
int Unpressed()
  int self, expression, file, subtype, worked, child, index
  int attached, chance, damage, oldtarget
  int items_to_destroy, i, item, price, oldcomp


  self = FindSelf()
  ButtonHandled()


  if(CharacterScriptFile(FindIndex(self)) != "FILE:CPLAYER.RUN")
    return FALSE


  if(self.action == ACTION_SPECIAL_10)
    // Too little time...
    self.action = ACTION_SPECIAL_12
    self.daction = ACTION_STAND
    return FALSE


  if(self.action == ACTION_SPECIAL_11)
    self.action = ACTION_SPECIAL_12
    self.daction = ACTION_STAND


    expression = ITALK.ExpressionFromAxis(self.baxis)
    if(expression == 0)
      // !!!BAD!!!
      // !!!BAD!!! Do voice here...
      // !!!BAD!!!
      return FALSE


    chance = ChanceOfWorking(expression, self.int)
    if(chance <= 0)
      // !!!BAD!!!
      // !!!BAD!!! Do voice here...
      // !!!BAD!!!
      BuzzerSound()
      return FALSE



    // If we're doing an item, make sure we have a free slot before
    // we haul off and explode ourself...
    worked = TRUE
    if(expression == 1 || expression == 2 || expression == 3 || expression == 4)
      worked = GiveItemToCharacter(self, ITEM_NONE)
    else
      // Twiddle compassion, since badge will disallow helpers...
      // But these helpers are okay...
      oldcomp = self.vircomp
      self.vircomp = 0
      worked = (ChildCount(self) < MaxHelpers(self, 0))
      self.vircomp = oldcomp
    if(worked)
      // Let's see if we blew ourself up...
      // Use 20 sided die becaue 100 sided gets skew'd results...
      if((RollDice(1, 20)*5) > chance)
        // We blew ourself up...
        damage = DamageIfFailed(expression)
        PEXPLODE.Effect(self.x, self.y, self.z, 0.0, 0.0, 3.34, DAMAGE_FIRE, 0, 0, FindIndex(self), self.team, "FILE:FIRE1.RGB", SMOKE_COLOR, 255)
        if(damage >= self.hits)
          // Randomly discard inventory items...
          items_to_destroy = 4
          while(items_to_destroy > 0)
            i = RollDice(1, 16)-1
            item = FileReadByte(self, 224+i)
            price = SystemGet(SYS_ITEMREGISTRYPRICE, item, 0)
            if(price > 0 && item != ITEM_GOLD_BAR && item != ITEM_ONE_UP)
              FileWriteByte(self, 224+i, ITEM_NONE)
            items_to_destroy--
        oldtarget = self.target
        self.target = FindIndex(self)
        DamageTarget(DAMAGE_BASH, 0, damage)
        self.target = oldtarget
      else
        // We are a pretty good tinker after all...
        file = NULL
        subtype = 0
        worked = FALSE


        if(expression == 1)
          worked = GiveItemToCharacter(self, ITEM_AMMO_10)
        if(expression == 2)
          worked = GiveItemToCharacter(self, ITEM_GONNE)
        if(expression == 3)
          worked = GiveItemToCharacter(self, ITEM_GRONNADE)
        if(expression == 4)
          worked = GiveItemToCharacter(self, ITEM_FLAMER)
        if(expression == 5)
          file = "FILE:CTANKE.RUN"
          subtype = 0
        if(expression == 6)
          file = "FILE:CKART.RUN"
          subtype = 128
        if(expression == 7)
          file = "FILE:CTANKE.RUN"
          subtype = 128
        if(expression == 8)
          file = "FILE:CCOPTER.RUN"


        if(file)
          SetSpawnOwner(FindIndex(self))
          SetSpawnSubtype(subtype)
          SetSpawnTeam(self.team)
          child = Spawn(CHARACTER, self.x, self.y, self.z, file)
          if(child)
            FixCharacterLocation(child)
            child.flatten = 64
            SetupTargetMatrix(child)
            child.spin = self.spin


            // Funky thing for HedgeHog turret...
            if(expression == 5)
              index = AcquireTarget(ACQUIRE_SEE_FRIENDLY | ACQUIRE_SEE_NEUTRAL | ACQUIRE_SEE_BEHIND, "FILE:CMINIGON.RUN", 5.0)
              attached = FindByIndex(index)
              if(attached)
                attached.frame = 1
                attached.action = ACTION_STAND
                attached.daction = ACTION_STAND
                attached.flatten = 64
                SetupTargetMatrix(attached)
            worked = TRUE
      if(worked)
        // We built our tinker thing...
        // !!!BAD!!!
        // !!!BAD!!!
        // !!!BAD!!!
        DebugMessage("Tinker'd okay")
        ButtonSound()
      else
        // Weird failure...  Too many characters in room or something...
        // !!!BAD!!!
        // !!!BAD!!!
        // !!!BAD!!!
        DebugMessage("Too many characters")
        BuzzerSound()
    else
      // Out of inventory space or too many helpers...
      // !!!BAD!!!
      // !!!BAD!!!
      // !!!BAD!!!
      DebugMessage("Out of inventory space/children")
      BuzzerSound()




//-----------------------------------------------------------------------
