#define button0  self.stat00l
#define button1  self.stat00h
#define button2  self.stat01l
#define button3  self.stat01h
#define button4  self.stat02l
#define button5  self.stat02h
#define button6  self.stat03l
#define button7  self.stat03h
#define button8  self.stat04l
#define button9  self.stat04h
#define button10 self.stat05l
#define editmode self.stat05h
#define kanji    self.stat06w

#define KEY4      92
#define KEYLEFT   80
#define KEY6      94
#define KEYRIGHT  79


//-----------------------------------------------------------------------
ClearButtons()
  int self
  self = FindSelf()
  button0 = 0
  button1 = 0
  button2 = 0
  button3 = 0
  button4 = 0
  button5 = 0
  button6 = 0
  button7 = 0
  button8 = 0
  button9 = 0
  button10 = 0

//-----------------------------------------------------------------------
// Keeps a button held down...
int StickyButton(int value)
  if(value == 255)
    ButtonSound()
    ClearButtons()
  if(value == 250)
    value = 251
  return value


//-----------------------------------------------------------------------
Spawn()
  int self
  self = FindSelf()
  ClearButtons()
  kanji = 0
  button0 = 255
  editmode = 0

//-----------------------------------------------------------------------
Refresh()
  int self
  self = FindSelf()


  // Button Actions...
  if(button0 == 255)
    editmode = 0
  if(button1 == 255)
    editmode = 1
  if(button2 == 255)
    editmode = 2
  if(button3 == 255)
    editmode = 3
  button0 = StickyButton(button0)
  button1 = StickyButton(button1)
  button2 = StickyButton(button2)
  button3 = StickyButton(button3)
  if(button4 == 250)
    UpdateFiles(UPDATE_SDFSAVE)
    ButtonSound()
  if(button5 == 250 || SystemGet(SYS_KEYPRESSED, KEYLEFT, 0) || SystemGet(SYS_KEYPRESSED, KEY4, 0))
    if(kanji > 0)
      kanji--
    ButtonSound()
  if(button6 == 250 || SystemGet(SYS_KEYPRESSED, KEYRIGHT, 0) || SystemGet(SYS_KEYPRESSED, KEY6, 0))
    if(kanji < (SystemGet(SYS_NUMKANJI, 0, 0)-1))
      kanji++
    ButtonSound()
  if(button7 == 250)
    SystemSet(SYS_KANJICOPY, kanji, 0, 0)
    ButtonSound()
  if(button8 == 250)
    SystemSet(SYS_KANJIPASTE, kanji, 0, 0)
    if(kanji < (SystemGet(SYS_NUMKANJI, 0, 0)-1))
      kanji++
    ButtonSound()
  if(button9 == 250)
    SystemSet(SYS_KANJIDELETE, kanji, 0, 0)
    if(kanji >= SystemGet(SYS_NUMKANJI, 0, 0))
      kanji--
    ButtonSound()
  if(button10 == 250)
    ButtonSound()
    GoPoof(POOF_SELF)



  // Title bar
  TitleBar(TRUE, 0.0, -10.0, "Kanji Tool")


  // Main window
  WindowBorder(FALSE,-6.0, -10.0, 20, 20, BORDER_FULL)
  WindowEditKanji(editmode, 0.0, 0.0, 20.0, kanji)


  // Button window...
  WindowBorder(FALSE,-15.0, -10.0, 8, 20, BORDER_FULL)
  button0 = ButtonImage(button0, 0.0, 0.0, 8, 2, "Add Pnt")
  button1 = ButtonImage(button1, 0.0, 2.0, 8, 2, "Add Tri")
  button2 = ButtonImage(button2, 0.0, 4.0, 8, 2, "Mov Pnt")
  button3 = ButtonImage(button3, 0.0, 6.0, 8, 1, "Del Pnt")
  button4 = ButtonImage(button4, 0.0, 7.0, 8, 1, "SDFSave")
  button5 = ButtonImage(button5, 0.0, 8.0, 4, 2, "<<")
  button6 = ButtonImage(button6, 4.0, 8.0, 4, 2, ">>")
  StringClear(String(0))
  StringAppendNumber(String(0), kanji, 256)
  StringAppend(String(0), "/", 256)
  StringAppendNumber(String(0), SystemGet(SYS_NUMKANJI, 0, 0), 256)
  WindowString(WHITE, 0.5 * (8 - StringLength(String(0))), 10.0, String(0))
  StringClear(String(0))
  StringAppendNumber(String(0), SystemGet(SYS_NUMKANJITRIANGLE, kanji, 0), 256)
  StringAppend(String(0), " Tri", 256)
  WindowString(WHITE, 0.5 * (8 - StringLength(String(0))), 11.0, String(0))
  button7 = ButtonImage(button7, 0.0,12.0, 8, 2, "Copy")
  button8 = ButtonImage(button8, 0.0,14.0, 8, 2, "Add New")
  button9 = ButtonImage(button9, 0.0,16.0, 8, 2, "Delete")
  button10= ButtonImage(button10,0.0,18.0, 8, 2, "GoPoof")


//-----------------------------------------------------------------------
