#define CHEAT FALSE // (SystemGet(SYS_KEYDOWN, 282, 0))  // F1
#define SHOW_STAT_SUM FALSE


#define rand_seed      self.stat03l
#define hits           self.stat03h
#define mana           self.stat04l
#define strength       self.stat04h
#define dexterity      self.stat05l
#define intelligence   self.stat05h
#define roll_button    self.stat06l
#define unroll_button  self.stat06h
#define name_lock      self.stat07l
#define class_lock     self.stat07h
#define button1        self.stat08l
#define button2        self.stat08h
#define button3        self.stat09l
#define button4        self.stat09h
#define player         self.stat12l
#define class_list     self.stat13w
#define camx           self.stat14w
#define petrify        self.stat15w
#define num_class      self.stat16l
#define monster_mode   self.stat16h        
#define frame          self.stat17w
#define slot           self.stat18w

#define character_name self.string1


#define ACTION_TO_PLAY ACTION_STAND
#define ALTERNATE_ACTION ACTION_JUMP


// !!!MAKE SURE YOU ADD A NAME TO THE LIST IF YOU CHANGE THIS!!!
// !!!REMEMBER THE COMMA!!!
#define MAX_MONSTER_CLASS 26


// Model setup functions use team to determine character's eyes...
#define team           self.team

//-----------------------------------------------------------------------
PreviewString(int amount, int index, float x, float y)
  int length
  StringClear(String(0))
  StringAppend(String(0), StringLanguage(index), 9)
  length = StringLength(String(0))
  if(amount > 9)
    length++
  if(amount > 99)
    length++
  while(length < 7)
    StringAppend(String(0), " ", 9)
    length++
  StringAppendNumber(String(0), amount, 9)
  WindowString(WHITE, x, y, String(0))


//-----------------------------------------------------------------------
CorrectString(int string_to_correct)
  // Removes nonstandard characters from string...
  int i, value


  i = 0
  value = StringGetValue(string_to_correct, i)
  if(value >= 'a' && value <= 'z')
    value = value - 32
    StringSetValue(string_to_correct, i, value)
  while(i < 15)
    value = StringGetValue(string_to_correct, i)
    if(value > 0)
      if(value == 32)
        i++
        if(i < 15)
          value = StringGetValue(string_to_correct, i)
          if(value >= 'a' && value <= 'z')
            value = value - 32
            StringSetValue(string_to_correct, i, value)
        i--
      else
        if(value != 39 && value != '-')
          if(value < 65 || value > 122 || (value > 90 && value < 97))
            StringSetValue(string_to_correct, i, 32)
      i++ 
    else
      i = 16


//-----------------------------------------------------------------------
UnspaceString(int string_to_unspace)
  // Removes spaces from start & end of string...
  int i, value


  while(StringGetValue(string_to_unspace, 0) == 32)
    StringChopLeft(string_to_unspace, 1)


  i = StringLength(string_to_unspace)-1
  while(i > 0 && StringGetValue(string_to_unspace, i)==32)
    StringChopRight(string_to_unspace, 1)
    i--

//-----------------------------------------------------------------------
Spawn()
  int self
  self = FindSelf()
  player = self.z
  team = TEAM_GOOD
  petrify = 0
  
  // Set up the preview model...
  camx = RollDice(1, 200)
  frame = 0
  
  // Fill in defaults
  hits = 0
  mana = 50
  strength = 0
  dexterity = 0
  intelligence = 0
  roll_button = 0
  unroll_button = 0
  button1 = 0
  button2 = 0
  button3 = 0
  button4 = 0

//-----------------------------------------------------------------------
int ClassSetup(int class, int getscript)
  int modelfile
  int scriptfile
  int self


  self = FindSelf()
  ModelClear()
  if(class == CLASS_GNOME || class == CLASS_DWARF || class == CLASS_HUBBIN)
    modelfile = "FILE:PLAYERSM.RDY"
  else
    modelfile = "FILE:PLAYER.RDY"
  scriptfile = "FILE:CPLAYER.RUN"
  self.class = class
  CPLAYER.ModelSetup()
  if(getscript)
    return scriptfile
  return modelfile


//-----------------------------------------------------------------------
int MonsterSetup(int class, int getscript)
  int modelfile
  int scriptfile

  ModelClear()
  modelfile = "FILE:PLAYER.RDY"
  scriptfile = "FILE:CPLAYER.RUN"
  if(class == 0)
    modelfile = "FILE:PORCANIM.RDY"
    scriptfile = "FILE:CPORC.RUN"
    CPORC.ModelSetup()
  if(class == 1)
    modelfile = "FILE:LUMPANIM.RDY"
    scriptfile = "FILE:CLUMPKIN.RUN"
    CLUMPKIN.ModelSetup()
  if(class == 2)
    modelfile = "FILE:CARRION.RDY"
    scriptfile = "FILE:CCARRION.RUN"
    CCARRION.ModelSetup()
  if(class == 3)
    modelfile = "FILE:ZOMBANIM.RDY"
    scriptfile = "FILE:CZOMBI.RUN"
    CZOMBI.ModelSetup()
  if(class == 4)
    modelfile = "FILE:WHELP.RDY"
    scriptfile = "FILE:CWHELP.RUN"
    CWHELP.ModelSetup()
  if(class == 5)
    modelfile = "FILE:DRAGON.RDY"
    scriptfile = "FILE:CDRAGON.RUN"
    CDRAGON.ModelSetup()
  if(class == 6)
    modelfile = "FILE:BATANIM.RDY"
    scriptfile = "FILE:CBAT.RUN"
    CBAT.ModelSetup()
  if(class == 7)
    modelfile = "FILE:RAT.RDY"
    scriptfile = "FILE:CRAT.RUN"
    CRAT.ModelSetup()
  if(class == 8)
    modelfile = "FILE:KITTANIM.RDY"
    scriptfile = "FILE:CKITTEN.RUN"
    CKITTEN.ModelSetup()
  if(class == 9)
    modelfile = "FILE:GRUBBUG.RDY"
    scriptfile = "FILE:CGRUBBUG.RUN"
    CGRUBBUG.ModelSetup()
  if(class == 10)
    modelfile = "FILE:GRUBMUM.RDY"
    scriptfile = "FILE:CGRUBMUM.RUN"
    CGRUBMUM.ModelSetup()
  if(class == 11)
    modelfile = "FILE:GREMF.RDY"
    scriptfile = "FILE:CGREMF.RUN"
    CGREMF.ModelSetup()
  if(class == 12)
    modelfile = "FILE:TRANCH.RDY"
    scriptfile = "FILE:CTRANCH.RUN"
    CTRANCH.ModelSetup()
  if(class == 13)
    modelfile = "FILE:WRAITHE.RDY"
    scriptfile = "FILE:CWRAITHE.RUN"
    CWRAITHE.ModelSetup()
  if(class == 14)
    modelfile = "FILE:ROACHIE.RDY"
    scriptfile = "FILE:CROACHIE.RUN"
    CROACHIE.ModelSetup()
  if(class == 15)
    modelfile = "FILE:WARLOCK.RDY"
    scriptfile = "FILE:CWARLOCK.RUN"
    CWARLOCK.ModelSetup()
  if(class == 16)
    modelfile = "FILE:OGREBOD0.RDY"
    scriptfile = "FILE:COGRE.RUN"
    COGRE.ModelSetup()
  if(class == 17)
    modelfile = "FILE:CHEST.RDY"
    scriptfile = "FILE:CCHEST.RUN"
    CCHEST.ModelSetup()
  if(class == 18)
    modelfile = "FILE:PLAYER.RDY"
    scriptfile = "FILE:CMEDUSA.RUN"
    CMEDUSA.ModelSetup()
  if(class == 19)
    modelfile = "FILE:BATANIM.RDY"
    scriptfile = "FILE:CEYEBAT.RUN"
    CEYEBAT.ModelSetup()
  if(class == 20)
    modelfile = "FILE:LIZZANIM.RDY"
    scriptfile = "FILE:CLIZZER.RUN"
    CLIZZER.ModelSetup()
  if(class == 21)
    modelfile = "FILE:WUFFANIM.RDY"
    scriptfile = "FILE:CWUFF.RUN"
    CWUFF.ModelSetup()
  if(class == 22)
    modelfile = "FILE:LURKER.RDY"
    scriptfile = "FILE:CLURKER.RUN"
    CLURKER.ModelSetup()
  if(class == 23)
    modelfile = "FILE:SLIME.RDY"
    scriptfile = "FILE:CSLIME.RUN"
    CSLIME.ModelSetup()
  if(class == 24)
    modelfile = "FILE:WISP.RDY"
    scriptfile = "FILE:CWISP.RUN"
    CWISP.ModelSetup()
  if(class == 25)
    modelfile = "FILE:SKULANIM.RDY"
    scriptfile = "FILE:CSKULTON.RUN"
    CSKULTON.ModelSetup()
  if(getscript)
    return scriptfile
  return modelfile

//-----------------------------------------------------------------------
Event()
  int self, child

  self = FindSelf()
  
  SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_CLASS, self.class)
  SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_TEAM, self.team)
  SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_SUBTYPE, self.eqcol01)

  // Let the player control this character...
  child = SystemGet(SYS_LOCALPLAYER, player, 0)
  SystemSet(SYS_LOCALPLAYER, player, 0, child)


//-----------------------------------------------------------------------
Refresh()
  int    i, self, child
  int    class_string, class, class_name
  int    openfile, scriptfile
  int    first_frame, max_frame
  int    skip
  int    read_offset, read_value
  int    action, weapon_frame
  int    player_character
  float  ratio

  // Allow us to save data in the window's properties...
  self = FindSelf()

  // screen aspect ratio
  ratio = SystemGet(SYS_SCREENPARAMS, 0, 2)
  ratio = ratio / SystemGet(SYS_SCREENPARAMS, 0, 3)

  // Find the player's character
  player_character = SystemGet(SYS_LOCALPLAYER, player, 0)
  if(player_character)
    SystemSet(SYS_RESPAWNCHARACTER, 0, 0, player_character)
    child = FindByIndex(player_character)
  
  monster_mode = SystemGet(SYS_CHARACTERSCRIPTFILE, 0, player_character) != "FILE:CPLAYER.RUN"

   // Build the string for the class drop down box...
  class = 0
  if(child)
    class = child.class
  class_string = String(1)
  if(monster_mode)
    // We're allowing the user to pick monster types...
    num_class = MAX_MONSTER_CLASS
    StringClear(class_string)
    StringAppend(class_string, "Porc,", 256)
    StringAppend(class_string, "Lumpkin,", 256)
    StringAppend(class_string, "Carrion,", 256)
    StringAppend(class_string, "Zombi,", 256)
    StringAppend(class_string, "Whelp,", 256)
    StringAppend(class_string, "Dragon,", 256)
    StringAppend(class_string, "Bat,", 256)
    StringAppend(class_string, "Rat,", 256)
    StringAppend(class_string, "Kitten,", 256)
    StringAppend(class_string, "Bug,", 256)
    StringAppend(class_string, "Mum,", 256)
    StringAppend(class_string, "Gremf,", 256)
    StringAppend(class_string, "Tranch,", 256)
    StringAppend(class_string, "Wraithe,", 256)
    StringAppend(class_string, "Roachie,", 256)
    StringAppend(class_string, "Warlock,", 256)
    StringAppend(class_string, "Ogre,", 256)
    StringAppend(class_string, "Mimic,", 256)
    StringAppend(class_string, "Medusa,", 256)
    StringAppend(class_string, "EyeBat,", 256)
    StringAppend(class_string, "Lizzer,", 256)
    StringAppend(class_string, "Wuff,", 256)
    StringAppend(class_string, "Lurker,", 256)
    StringAppend(class_string, "Slime,", 256)
    StringAppend(class_string, "Wisp,", 256)
    StringAppend(class_string, "Skulton,", 256)
  else
    // Only allow normal player classes...
    num_class = 2
    if(SystemGet(SYS_PAYINGCUSTOMER, 0, 0))
      num_class = 8
    StringClear(class_string)
    i = 0
    while(i < num_class)
      if(i == 4 && class == CLASS_ELF && (self.eqcol01&128))
        StringAppend(class_string, StringLanguage(107), 256)
      StringAppend(class_string, StringLanguage(84+i), 256)
      StringAppend(class_string, ",", 256)
      i++


  // Title bar
  StringClear(String(0))
  StringAppend(String(0), StringLanguage(3), 256)
  StringAppendNumber(String(0), player+1, 256)
  TitleBar(TRUE, 0.0, -5.5, String(0))

  // Main window
  WindowBorder(FALSE, -10.0, -5.5, 10 + 7.5 * ratio, 11, BORDER_FULL)


  // Setup the preview model...
  if(child)
    self.eqcol01 = child.eqcol01
    self.eqcol23 = child.eqcol23
    self.eqhelm = child.eqhelm
    self.eqbody = child.eqbody
    self.eqlegs = child.eqlegs
    self.eqleft = child.eqleft
    self.eqright = child.eqright
    self.class = child.class
  
  // Find the name of the selected class (by searching the drop down box string)
  i = 0
  class_name = class_string
  read_offset = 0
  read_value = StringGetValue(class_string, 0)
  while(i < class && read_offset < 255 && read_value != 0)
    if(read_value == ',')
      class_name = class_string+read_offset+1
      i++
    read_offset++
    read_value = StringGetValue(class_string, read_offset)
  StringClear(String(4))
  StringAppend(String(4), class_name, 16)
  class_name = String(4)
  i = 0
  while(i < 16)
    read_value = StringGetValue(class_name, i)
    if(read_value == ',' || read_value == 0)
      StringSetValue(class_name, i, 0)
    i++
  
  // Check for character type
  openfile = 0
  scriptfile = 0
  
  if(player_character)
    if(!monster_mode)
      // Character is human, load normal model
      openfile = ClassSetup(class, FALSE)
      scriptfile = ClassSetup(class, TRUE)
    // Character is monster, load the appropriate monster model
    else
      openfile = MonsterSetup(class, FALSE)
      scriptfile = MonsterSetup(class, TRUE)
    
    


  // Animate the model...
  action = ACTION_TO_PLAY
  first_frame = SystemGet(SYS_MODELACTIONSTART, openfile, action)
  if(first_frame == 65535)
    action = ALTERNATE_ACTION
    first_frame = SystemGet(SYS_MODELACTIONSTART, openfile, action)
  if(first_frame == 65535)
    frame = 0
  else
    max_frame = SystemGet(SYS_MODELMAXFRAME, openfile, 0)
    if(frame < first_frame)
      frame = first_frame
    skip = SystemGet(SYS_MAINFRAMESKIP, 0, 0)
    while(skip > 0)
      frame++
      skip--
    if(frame >= max_frame)
      frame = first_frame
    if(SystemGet(SYS_MODELFRAMEACTIONNAME, openfile, frame) != action)
      frame = first_frame


  // Preview window
  WindowBorder(FALSE, 0.0, -2.5, 7.5 * ratio, 8, BORDER_FULL)
  Window3DStart(0.0, 0.0, 7.5 * ratio, 8.0, FALSE)
  Window3DPosition(0.0, 0.0, 3.25, WIN_TARGET)
  Window3DPosition(sin(camx)*4.0, 3.25, 6.25, WIN_CAMERA)
  Window3DModel(WIN_3D_MODEL, MODEL_BASE_FILE, frame, 255)
  Window3DModel(WIN_3D_SHADOW, MODEL_BASE_FILE, frame, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_LEGS_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_CHEST_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_ARMS_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_PADS_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_FEET_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_HEAD_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_HAIR_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_MOUTH_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_EYES_FILE, 0, 255)
  weapon_frame = SystemGet(SYS_MODELFRAMEFLAGS, openfile, frame) >> 7
  Window3DModel(WIN_3D_MODEL, MODEL_LEFT_FILE, weapon_frame, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_RIGHT_FILE, weapon_frame, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_LEFT2_FILE, weapon_frame, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_RIGHT2_FILE, weapon_frame, 255)

  Window3DEnd()
  skip = SystemGet(SYS_MAINFRAMESKIP, 0, 0)
  while(skip > 0)
    camx++
    if(camx >= 360)
      camx = 0
    skip--


  // Go back to the main window...
  WindowBorder(FALSE, -10.0, -5.5, 20, 11, BORDER_NONE)



  // Preview stats
  if(child)
    hits = child.hitsmax
    mana = child.manamax
    strength = child.str
    dexterity = child.dex
    intelligence = child.int
    PreviewString(hits, 50, 0.0, 6.0)
    PreviewString(mana, 51, 0.0, 7.0)
    PreviewString(strength, 55, 0.0, 8.0)
    PreviewString(dexterity, 56, 0.0, 9.0)
    PreviewString(intelligence, 57, 0.0, 10.0)
  if(SystemGet(SYS_DEVTOOL, 0, 0) && CHEAT && SHOW_STAT_SUM)
    StringClear(String(0))
    StringAppendNumber(String(0), hits+mana+strength+dexterity+intelligence, 256)
    WindowString(WHITE, 10.0, 8.0, String(0))
    StringClear(String(0))


  // Show character name
  if(child)
    StringClear(character_name)
    StringAppend(character_name, child+144, 16)
    WindowString(WHITE, 0.0, 0.0, character_name)
    CorrectString(character_name)


    // Show class name
    WindowString(WHITE, 0.0, 1.0, class_name)


//-----------------------------------------------------------------------
